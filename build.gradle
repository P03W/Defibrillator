plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.5.0'
    id 'net.minecrell.licenser' version "0.4.1"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        url 'https://jitpack.io'
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroupByRegex 'io\\.github\\.onyxstudios.*'
        }
    }
    mavenLocal()
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // FLK
    modImplementation "net.fabricmc:fabric-language-kotlin:1.6.0+kotlin.1.5.0"

    // Aegis
    modImplementation 'com.github.P03W:Aegis:3.0.0'
    include 'com.github.P03W:Aegis:3.0.0'

    // Microconfig
    modImplementation 'com.github.P03W:Microconfig:2.0.1'
    include 'com.github.P03W:Microconfig:2.0.1'

    // Ne-V-Seti
    modImplementation 'com.github.BasiqueEvangelist:ne-v-seti:-SNAPSHOT'
    include 'com.github.BasiqueEvangelist:ne-v-seti:-SNAPSHOT'

    // QueCee
    modImplementation 'com.github.p03w:quecee:1.0.3'
    include 'com.github.p03w:quecee:1.0.3'

    // Player Ability Lib
    modImplementation "io.github.ladysnake:PlayerAbilityLib:1.2.2"
    include "io.github.ladysnake:PlayerAbilityLib:1.2.2"
}

// Licenser
license {
    header = rootProject.file('LICENSE_FILE_HEADER')
    include '**/*.java'
    include '**/*.kt'
    it.style.java = 'BLOCK_COMMENT'
    it.style.kt = 'BLOCK_COMMENT'
}

processResources {
    inputs.property "version", project.version
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"

    it.options.release = 16
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "16"

        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }
}

compileKotlin.kotlinOptions.jvmTarget = "16"
